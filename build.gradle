buildscript {
	repositories {
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		maven { url ='https://dl.bintray.com/kotlin/kotlin-eap-1.1' }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE")
		classpath 'io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE'
		classpath "net.ltgt.gradle:gradle-apt-plugin:0.7"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1-M02"
	}
}

subprojects {
	apply plugin: "groovy"
	apply plugin: 'io.spring.dependency-management'
	apply plugin: "idea"
	apply plugin: "kotlin"

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		jcenter()
		maven { url ='https://dl.bintray.com/kotlin/kotlin-eap-1.1' }
	}

	dependencyManagement {
		imports {
			mavenBom 'io.spring.platform:platform-bom:Athens-RELEASE'
		}
	}

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib:1.1-M02"
		testCompile "org.spockframework:spock-core:1.1-groovy-2.4-rc-2"
		testCompile "org.codehaus.groovy:groovy-all:2.4.7"
		testCompile 'cglib:cglib-nodep:3.2.4'
		testCompile 'org.objenesis:objenesis:2.4'
	}
}

project(":app-api") {
}

project(":app-impl") {
	dependencies {
		compile project(":app-api")
		compile project(":domain")
	}
}

project(":domain") {
}

project(":infra-rest") {
	dependencies {
		compile project(":app-api")

		compile "org.springframework:spring-webmvc"
		compile "com.fasterxml.jackson.core:jackson-databind"
//		compile "com.fasterxml.jackson.module:jackson-module-kotlin"

		testCompile 'com.jayway.restassured:spring-mock-mvc:2.9.0'
		testCompile 'javax.servlet:javax.servlet-api:3.1.0'
		testCompile 'commons-logging:commons-logging:1.2'
	}
}

project(":infra-db-jpa") {
	dependencies {
		compile project(":domain")
		compile "org.springframework.data:spring-data-jpa"
		compile "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final"
	}
}

project(":infra-mongodb") {
	dependencies {
		compile project(":domain")
		compile 'org.mongodb.morphia:morphia:1.2.1'
	}
}

project(":infra-db-jdbc") {
	dependencies {
		compile project(":domain")
		compile 'org.springframework:spring-jdbc'
	}
}

project(":springboot-deployment") {
	apply plugin: "spring-boot"

	dependencies {
		compile project(":domain")
		compile project(":app-api")
		compile project(":app-impl")
		compile project(":infra-db-jpa")
		compile project(":infra-db-jdbc")
		compile project(":infra-mongodb")
		compile project(":infra-rest")

		compile "org.springframework.boot:spring-boot-starter-web"
		compile "org.springframework.boot:spring-boot-starter-data-jpa"
		compile "org.springframework.boot:spring-boot-actuator"

		compile "com.h2database:h2"
		compile 'com.github.fakemongo:fongo:2.0.9'

		testCompile "org.spockframework:spock-spring:1.1-groovy-2.4-rc-2"
		testCompile "org.springframework.boot:spring-boot-starter-test"
	}
}