buildscript {
	repositories {
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE")
		classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
		classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.7"
	}
}

subprojects {
	apply plugin: "java"
	apply plugin: "io.spring.dependency-management"
	apply plugin: "idea"

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		jcenter()
	}

	dependencyManagement {
		imports {
			mavenBom "io.spring.platform:platform-bom:Athens-RELEASE"
		}
	}

	dependencies {
		compile "com.google.code.findbugs:jsr305:3.0.1"
		testCompile 'junit:junit:4.12'
		testCompile 'org.assertj:assertj-core:3.6.1'
	}
}

project(":app-api") {
}

project(":app-impl") {
	dependencies {
		compile project(":app-api")
		compile project(":domain")
		compile 'javax.transaction:javax.transaction-api:1.2'

		testCompile "org.mockito:mockito-core"
	}
}

project(":domain") {
}

project(":infra-rest-springmvc") {
	dependencies {
		compile project(":app-api")

		compile "org.springframework:spring-webmvc"
		compile "com.fasterxml.jackson.core:jackson-databind"

		testCompile "javax.servlet:javax.servlet-api:3.1.0"
		testCompile "commons-logging:commons-logging:1.2"
	}
}

project(":infra-rest-jersey") {
	dependencies {
		compile project(":app-api")

		compile "com.fasterxml.jackson.core:jackson-databind"
		compile "org.glassfish.jersey.core:jersey-server"

		testCompile "javax.servlet:javax.servlet-api:3.1.0"
		testCompile "commons-logging:commons-logging:1.2"
	}
}

project(":infra-db-jpa") {
	dependencies {
		compile project(":domain")
		compile "org.springframework.data:spring-data-jpa"
		compile "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final"
	}
}

project(":infra-mongodb") {
	dependencies {
		compile project(":domain")
		compile "org.mongodb.morphia:morphia:1.2.1"
	}
}

project(":infra-db-jdbi") {
	dependencies {
		compile project(":domain")
		compile "org.jdbi:jdbi:2.77"
	}
}

project(":springboot-deployment") {
	apply plugin: "spring-boot"

	dependencies {
		compile project(":domain")
		compile project(":app-api")
		compile project(":app-impl")
		compile project(":infra-db-jpa")
		compile project(":infra-rest-jersey")

		compile "org.springframework.boot:spring-boot-starter-jersey"
		compile "org.springframework.boot:spring-boot-actuator"
		compile "org.springframework.boot:spring-boot-starter-data-jpa"

		compile "com.h2database:h2"

		testCompile "org.springframework.boot:spring-boot-starter-test"
		testCompile "com.jayway.restassured:spring-mock-mvc:2.9.0"
	}
}
