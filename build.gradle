buildscript {
	repositories {
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE")
		classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
		classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.7"
                classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
                classpath 'org.owasp:dependency-check-gradle:2.1.1'
	}
}

subprojects {
	apply plugin: "java"
	apply plugin: "io.spring.dependency-management"
	apply plugin: "idea"
        apply plugin: 'com.github.ben-manes.versions'
        apply plugin: 'org.owasp.dependencycheck'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		jcenter()
	}

	dependencyManagement {
		imports {
			mavenBom "io.spring.platform:platform-bom:Athens-RELEASE"
		}
	}

	dependencies {
		compile "com.google.code.findbugs:jsr305:3.0.1"
		testCompile 'junit:junit:4.12'
		testCompile 'org.assertj:assertj-core:3.6.1'
	}
}

project(":app-api") {
}

project(":app-impl") {
	dependencies {
		compile project(":app-api")
		compile project(":domain")
		compile 'javax.transaction:javax.transaction-api:1.2'
		compile 'javax.inject:javax.inject:1'

		testCompile "org.mockito:mockito-core"
	}
}

project(":domain") {
}

project(":infra-rest-springmvc") {
	dependencies {
		compile project(":app-api")
		compile 'javax.inject:javax.inject:1'

		compile "org.springframework:spring-webmvc"
		compile "com.fasterxml.jackson.core:jackson-databind"

		testCompile "javax.servlet:javax.servlet-api:3.1.0"
		testCompile "commons-logging:commons-logging:1.2"
	}
}

project(":infra-rest-jersey") {
	dependencies {
		compile project(":app-api")
		compile 'javax.inject:javax.inject:1'

		compile "com.fasterxml.jackson.core:jackson-databind"
		compile "org.glassfish.jersey.core:jersey-server"

		testCompile "javax.servlet:javax.servlet-api:3.1.0"
		testCompile "commons-logging:commons-logging:1.2"
	}
}

project(":infra-json-rpc") {
	dependencies {
		compile project(":app-api")
		compile "org.springframework:spring-web"
		compile "com.github.briandilley.jsonrpc4j:jsonrpc4j:1.2.0"
	}
}

project(":infra-graphql") {
	dependencies {
		compile project(":app-api")
		compile "com.graphql-java:graphql-spring-boot-starter:3.6.0"
		compile 'com.graphql-java:graphiql-spring-boot-starter:3.6.0'
		compile "com.graphql-java:graphql-java-tools:3.2.0"
	}
}

project(":infra-db-jpa") {
	dependencies {
		compile project(":domain")
		compile 'javax.inject:javax.inject:1'
		compile "org.springframework.data:spring-data-jpa"
		compile "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final"
	}
}

project(":infra-mongodb") {
	dependencies {
		compile project(":domain")
		compile 'javax.inject:javax.inject:1'
		compile "org.mongodb.morphia:morphia:1.2.1"
	}
}

project(":infra-db-jdbi") {
	dependencies {
		compile project(":domain")
		compile 'javax.inject:javax.inject:1'
		compile "org.jdbi:jdbi:2.77"
	}
}

project(":springboot-deployment") {
	apply plugin: "spring-boot"

	dependencies {
		compile project(":domain")
		compile project(":app-api")
		compile project(":app-impl")
		compile project(":infra-db-jpa")
		compile project(":infra-json-rpc")
		compile project(":infra-graphql")
		compile project(":infra-rest-springmvc")

		compile "org.springframework.boot:spring-boot-starter-web"
		compile "org.springframework.boot:spring-boot-actuator"
		compile "org.springframework.boot:spring-boot-starter-data-jpa"

		compile "com.h2database:h2"

		testCompile "org.springframework.boot:spring-boot-starter-test"
		testCompile "com.jayway.restassured:spring-mock-mvc:2.9.0"
	}
}
